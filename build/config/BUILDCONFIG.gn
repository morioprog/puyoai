declare_args() {
  os = host_os

  is_debug = true
  
  use_gui = false
  use_tcp = false
  use_httpd = false
}

# OS DEFINITIONS
is_posix = os == "mac" || os == "linux"
is_mac = os == "mac"
is_linux = os == "linux"
is_win = os == "win" || os == "winuwp"

if (os == "linux") {
  set_default_toolchain("//build/toolchain/linux:x64")
}

default_compiler_configs = [
  "//build/config/compiler",
  "//build/config/global",
]

if (is_debug) {
  default_compiler_configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
  ]
} else {
  default_compiler_configs += [
   "//build/config:release",
   "//build/config/compiler:optimize",
  ]
}

if (use_gui) {
  default_compiler_configs += [
    "//build/config:sdl2"
  ]
}

set_defaults("executable") {
  configs = default_compiler_configs
}

set_defaults("static_library") {
  configs = default_compiler_configs
}

set_defaults("shared_library") {
  configs = default_compiler_configs
}

set_defaults("source_set") {
  configs = default_compiler_configs
}

