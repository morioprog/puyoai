# OS definitions

declare_args() {
  os = host_os
}

is_posix = os == "mac" || os == "linux"
is_mac = os == "mac"
is_linux = os == "linux"
is_win = os == "win" || os == "winuwp"

declare_args() {
  # true if debug build.
  is_debug = true
  # true to enable ASan.
  is_asan = false
  # true to use clang instead of gcc.
  is_clang = is_posix

  # true to build capture.
  use_capture = false
  # true to build usb device.
  use_usb = false
  # true to build with gui (SDL2 is required)
  use_gui = false
  # true to build with httpd (microhttpd is required)
  use_httpd = false
  # true to build with curl (libcurl is required)
  use_libcurl = false
  # true to build with tcp socket.
  use_tcp = is_posix
  # true to build with curl (libcurl is required)
  use_curl = false
}

if (is_clang) {
  set_default_toolchain("//build/toolchain/$os:clang_x64")
} else {
  set_default_toolchain("//build/toolchain/$os:x64")
}

default_compiler_configs = [
  "//build/config/compiler",
  "//build/config:global",
]

if (is_debug) {
  default_compiler_configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
  ]
} else {
  default_compiler_configs += [
   "//build/config:release",
   "//build/config/compiler:optimize",
  ]
}

if (is_asan) {
  default_compiler_configs += [
    "//build/config:asan",
  ]
}

if (use_tcp) {
  default_compiler_configs += [
    "//build/config:use_tcp",
  ]
}

if (use_httpd) {
  default_compiler_configs += [
    "//build/config:use_httpd",
  ]
}

if (use_libcurl) {
  default_compiler_configs += [
    "//build/config:use_libcurl",
  ]
}

set_defaults("executable") {
  configs = default_compiler_configs
}

set_defaults("static_library") {
  configs = default_compiler_configs
}

set_defaults("shared_library") {
  configs = default_compiler_configs
}

set_defaults("source_set") {
  configs = default_compiler_configs
}

