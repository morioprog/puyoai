# OS definitions

declare_args() {
  os = host_os
}

is_posix = os == "mac" || os == "linux"
is_mac = os == "mac"
is_linux = os == "linux"
is_win = os == "win" || os == "winuwp"

declare_args() {
  is_debug = true
  
  use_capture = false
  use_usb = false
  use_gui = false
  use_httpd = false
  use_libcurl = false
  use_tcp = is_posix
}

if (is_linux) {
  set_default_toolchain("//build/toolchain/linux:x64")
}

if (is_mac) {
  set_default_toolchain("//build/toolchain/mac:x64")
}

default_compiler_configs = [
  "//build/config/compiler",
  "//build/config:global",
]

if (is_debug) {
  default_compiler_configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
  ]
} else {
  default_compiler_configs += [
   "//build/config:release",
   "//build/config/compiler:optimize",
  ]
}

if (use_tcp) {
  default_compiler_configs += [
    "//build/config:use_tcp",
  ]
}

if (use_httpd) {
  default_compiler_configs += [
    "//build/config:use_httpd",
  ]
}

if (use_libcurl) {
  default_compiler_configs += [
    "//build/config:use_libcurl",
  ]
}

set_defaults("executable") {
  configs = default_compiler_configs
}

set_defaults("static_library") {
  configs = default_compiler_configs
}

set_defaults("shared_library") {
  configs = default_compiler_configs
}

set_defaults("source_set") {
  configs = default_compiler_configs
}

